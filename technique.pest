// Parsing Expression Grammar for v1 of the Technique Procedure Language

WHITESPACE = _{ " " | "\t" }

technique = {
    SOI ~
    magic_line ~
    NEWLINE ~
    spdx_line? ~
    NEWLINE ~
    template_line? ~
    NEWLINE+ ~
    declaration ~
    NEWLINE+ ~
    EOI
}

// File Format Header

magic_line = { "%" ~ "technique" ~ "v1" }

// License and Copyright Header

spdx_line = { "!" ~ license ~ (";" ~ copyright)? }

license = @{ (ASCII_ALPHA | ASCII_DIGIT) ~ (ASCII_ALPHA | ASCII_DIGIT | "-" | "_" | " " | "." )* }

copyright = { ("Â©" | "(c)" | "(C)") ~ year? ~ owner }

year = @{ ASCII_DIGIT{4} ~ "-" ~ (ASCII_DIGIT{4})? | ASCII_DIGIT{4} }

owner = @{ (ASCII_ALPHA | ASCII_DIGIT) ~ (ASCII_ALPHA | ASCII_DIGIT | "-" | "_" | " " | "," | "." )* }

// Template Header

template_line = { "&" ~ template }

template = @{ (ASCII_ALPHA | ASCII_DIGIT) ~ (ASCII_ALPHA | ASCII_DIGIT | "-" | "_" | "." | "," )* }

// Procedure Declaration

declaration = { identifier ~ ":" ~ signature? }

identifier = @{ ASCII_ALPHA ~ (ASCII_ALPHA | ASCII_DIGIT | "_")* }

signature = { forma ~ ("," ~ forma )* ~ "->" ~ forma }

forma = @{ ASCII_ALPHA_UPPER ~ (ASCII_ALPHA | ASCII_DIGIT)* }




